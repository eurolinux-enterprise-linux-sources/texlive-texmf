%%%%%% rejected glyphs %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beginchar(106,26uw#+rm#,28uh#,0); "variant malayalam digit four";   
    pickup frame_pen;
    z1=(0uw, 0uh);
    z2=(0uw, 28uh);
    z3=.5[z1,z2];
    z4=(15uw, y3);
    z5=(20uw, .5[y3, y6]);
    z6=(15uw, 0uh);
    z7=(10uw, y5);
    z8=(26uw, 24uh);
    z9=(x8-6uw, 28uh);
    
    draw z1--z2;
    draw z3--z4{right}..z5{down}..z6{left}..z7{up}..z8{up}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(109,20uw#+rm#,25uh#,5uh#); "variant malayalam digit seven";   
    pickup frame_pen;
    transform t;
    picture p;
    t = identity rotatedaround ((0,0), -90) shifted (0, 25uh);
    x2=14uw;
    y4=0uh;
    eye(1,2,3,4)(12uw,12uh);
    z5=(10uw, 0);
    z6=(0, 10uh);
    z7=(12uw, 20uh);
    z8=(30uw, 0);

    draw_eye(1,2,3,4);
    draw z4..z5{left}..z6{up}..z7{right}..z8{down};
    p := currentpicture;
    clearit;
    currentpicture := p transformed t;
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(196,46uw#+rm#,20uh#,5uh#); "bh+dha";
    pickup frame_pen;
    x2=0;
    letter_bha(1,2,3,4,5,6,7,8,9,10,11,12');
    z12=z12'-(0, 3uh);
    z13=(.5[x12,x14], -5uh);
    x14=28uw;
    letter_dha(13',14',15',14,15,16,17,18);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11{left}..z12{down}..z13{right}..
        z14{up}---z15;
    draw z14{down}..z16{right}..z17{up}..z18;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

% version of ja with standard height (tail not hanging under base-
% line). This glyph looks only nice in normal style in high
% resolution, not at 300dpi.

def var_letter_ja(suffix a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r) =
% define points of "malayalam letter ja"
% x coordinates relative to x.a
% y coordinates absolute;
    % first eye
    z.a=(x.a, 13.5uh);
    eye(a,b,c,d)(7uw, 7uh);
    % first bow
    z.e=(x.a, 14uh);
    z.f=(x.a+7uw, 20uh);
    z.g=(x.a+14uw, 14uh);
    z.h=(x.g, 10.5uh);
    % second bow
    z.i=(x.a+20uw, 20uh);
    z.j=(x.a+26uw, 14uh);
    % tail
    z.k=(x.a+13uw, 7.5uh);
    z.l=(x.a+.5uw, 3uh);
    z.m=(x.a+6uw,  0uh);
    z.n=(x.a+14uw, 3uh);
    % second eye (diameter = 5)
    z.p=(x.a+23uw, 5uh);
    eye(o,p,q,r)(5uw, 5uh);
enddef;

beginchar(28,26uw#+rm#,20uh#,0); "U+0D1C malayalam letter ja";
    pickup frame_pen;
    x1 = 0uw;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}...z12{down}...
        z13{right}..z14..{right}z16;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

beginchar(162,58uw#+rm#,20uh#,0); "j+ja";
    pickup frame_pen;
    x1=0;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15',16',17',18');
    z14=(24uw, 5uh);
    x1a=32uw;
    var_letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}...z12{down}...
        z13{right}..z14..z1a{up};

    draw_eye(1a,2a,3a,4a);
    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a{left}...z12a{down}...
        z13a{right}..z14a..{right}z16a;
    draw_eye(15a,16a,17a,18a);
    
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
endchar;

beginchar(184,74uw#+rm#,20uh#,0); "j+nya";
    pickup frame_pen;
    x1 = 0uw;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15,16,17,18);
    z14=(24uw, 5uh);

    x1a=32uw;
    letter_nya(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);
    
    draw_eye(1a,2a,3a,4a);
    draw z4a{left}..z5a{up}..z6a{right}..z7a{down}---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a{left}..z12a{up}..
        z13a{right}..z14a{down}..z15a;
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}...z12{down}...
        z13{right}..z14..{right}z2a;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

% versions of ny+ca and sh+ca with little connecting eye

beginchar(185,60uw#+rm#,20uh#,0); "ny+ca";
    % link eye is same as in "sh+ca"
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=z9'+(2uw, 0);
    z10=z10'+(2uw, 1uh);
    x17=24uw;
    letter_ca(12',13',14,15,16,17,18,19);
    z12=(.5[x10,x13], y12');
    z13=(x13', y10'-1.25uh);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left};
    draw z17--z18 & z18--z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

beginchar(197,54uw#+rm#,ht#,0); "sh+ca";
    % link eye is same as in "ny+ca"
    pickup frame_pen;
    x2=0;
    letter_sha(1,2,3,4,5,6',7',8',9');
    z6=z6'+(0, 1uh);
    x15=22uw;
    letter_ca(10',11',12,13,14,15,16,17);
    z10=(.5[x6,x11], y10');
    z11=(x11', y6'-1.25uh);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z15--z16 & z16--z17;
    penlabels(1,2,3,4,5,6,10,11,12,13,14,15,16,17);
endchar;

% wide and big vowel signs i and ii

beginchar(63,2uw#+rm#,36uh#,0); "U+0D3F malayalam vowel sign i";
    pickup frame_pen;
    z1=(-14uw, 28uh);
    z2=(-6uw, 36uh);
    z3=(2uw, 25uh);
    z4=(2uw, 0);
    draw z1{up}..z2{right}..z3{down}---z4;
    penlabels(1,2,3,4);
endchar;

beginchar(64,2uw#+rm#,36uh#,0); "U+0D40 malayalam vowel sign ii";
    pickup frame_pen;
    z1=(-14uw, 28uh);
    z2=(-6uw, 36uh);
    z3=(2uw, 25uh);
    z4=(2uw, 0);
    eye(1,5,6,7)(8uw,8uh);
    draw_eye(1,5,6,7);
    draw z1{up}..z2{right}..z3{down}---z4;
    penlabels(1,2,3,4,5,6,7);
endchar;

% one of the many attempts...

beginchar(83,14uw#+rm#,20uh#,14uh#); "secondary ya";
    pickup frame_pen;
    z1=(10uw, 20uh);
    z2=(7uw, 10uh);
    z3=(14uw, -7uh);
    z4=(7uw, -14uh);
    z5=(0, -7uh);
    z6=(3uw, 0);
    draw z1..z2{down}..z3{down}..z4{left}..z5{up}..z6{up};
    penlabels(1,2,3,4,5,6);
endchar;

beginchar(72,10uw#+rm#,25uh#,0); "reformed script vowel sign u";
    pickup frame_pen;
    z1=(0uw, 25uh);
    z2=(3uw, 17.5uh);
    z5=(0, 5uh);
    eye(5,6,7,8)(10uw, 10uh);

    draw z1..z2{down}..z5{down};
    draw_eye(5,6,7,8);
    penlabels(1,2,5,6,7,8);
endchar;

beginchar(73,12uw#+rm#,25uh#,0); "reformed script vowel sign uu";
    pickup frame_pen;
    z1=(0uw, 25uh);
    z2=(3uw, 17.5uh);
    z5=(0, 6uh);
    eye(5,6,7,8)(12uw, 12uh);
    eye(9,6,10,11)(7uw, 7uh);

    draw z1..z2{down}..z5{down};
    draw_eye(5,6,7,8);
    draw_eye(9,6,10,11);
    penlabels(1,2,5,6,7,8,9,10,11);
endchar;

beginchar(74,14uw#+rm#,20uh#,0); "reformed script secondary ra";
    pickup frame_pen;
    z1=(14uw, 15uh);
    z2=(7uw, h);
    z3=(0, 10uh);
    z4=(x2, 0);
    z5=(x1, 5uh);
    draw z1..z2{left}..z3{down}..z4{right}..z5;
    penlabels(1,2,3,4,5);
endchar;

beginchar(47,32uw#+rm#,ht#,0); "U+0D2F malayalam letter ya";
    pickup frame_pen;
    z1=(10.5uw, 16uh);
    z2=(16uw, 20uh);
    z3=(22uw, 12uw);
    z4=(11uw, 0);
    z5=(0, 12uh);
    z6=(5uw, 19uh);
    z7=(13uw, 10uh);
    z8=(23uw, 0);
    z9=(32uw, 10uh);
    z10=(28uw, 20uh);
    draw z1..z2{right}..z3{down}..z4{left}..z5{up}..z6{right}..
       z7..z8{right}..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;
